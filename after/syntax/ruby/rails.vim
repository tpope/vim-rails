hi def link rubyEntity                      rubyMacro
hi def link rubyEntities                    rubyMacro
hi def link rubyExceptionMacro              rubyMacro
hi def link rubyValidation                  rubyMacro
hi def link rubyCallback                    rubyMacro
hi def link rubyRakeMacro                   rubyMacro
hi def link rubyTestMacro                   rubyMacro
hi def link rubyMacro                       Macro
hi def link rubyRoute                       rubyControl
hi def link rubySchema                      rubyControl
hi def link rubyResponse                    rubyControl
hi def link rubyAction                      rubyControl
hi def link rubyUrlHelper                   rubyHelper
hi def link rubyViewHelper                  rubyHelper
hi def link rubyTestHelper                  rubyHelper
hi def link rubyUserAssertion               rubyAssertion
hi def link rubyAssertion                   rubyException
hi def link rubyTestAction                  rubyControl
hi def link rubyHelper                      Function
hi def link rubyDebug                       Debug

let s:has_app = exists('*RailsDetect') && RailsDetect()
if s:has_app
  let s:path = '/' . rails#buffer().relative()
else
  let s:path = tr(expand('%:p'), '\', '/')
endif

if s:path =~# '\v/app/%(channels|controllers|helpers|jobs|mailers|models)/.*\.rb$|/app/views/'
  syn match rubyHelper '\v<%(logger)>[!?:]@!'
endif

if s:path =~# '/app/models/.*_observer\.rb$'
  syn match rubyMacro '\v<%(observe)>[!?:]@!'

elseif s:path =~# '/app/models/.*\.rb$'
  syn match rubyMacro '\v<%(accepts_nested_attributes_for|attr_readonly|attribute|enum|serialize|store|store_accessor)>[!?:]@!'
  syn match rubyMacro '\v<%(default_scope|scope)>[!?:]@!'
  syn match rubyEntity '\v<%(belongs_to|has_one|composed_of)>[!?:]@!'
  syn match rubyEntities '\v<%(has_many|has_and_belongs_to_many)>[!?:]@!'
  syn match rubyCallback '\v<%(before_validation|after_validation)>[!?:]@!'
  syn match rubyCallback '\v<%(before_create|before_destroy|before_save|before_update)>[!?:]@!'
  syn match rubyCallback '\v<%(after_create|after_destroy|after_save|after_update)>[!?:]@!'
  syn match rubyCallback '\v<%(around_create|around_destroy|around_save|around_update)>[!?:]@!'
  syn match rubyCallback '\v<%(after_commit|after_create_commit|after_update_commit|after_save_commit|after_destroy_commit|after_rollback)>[!?:]@!'
  syn match rubyCallback '\v<%(after_find|after_initialize|after_touch)>[!?:]@!'
  syn match rubyValidation '\v<%(validates|validates_acceptance_of|validates_associated|validates_confirmation_of|validates_each|validates_exclusion_of|validates_format_of|validates_inclusion_of|validates_length_of|validates_numericality_of|validates_presence_of|validates_absence_of|validates_size_of|validates_with)>[!?:]@!'
  syn match rubyValidation '\v<%(validates_associated|validates_uniqueness_of)>[!?:]@!'
  syn match rubyMacro '\v<%(validate|has_rich_text|has_secure_password|has_secure_token|has_one_attached|has_many_attached|delegated_type)>[!?:]@!'
endif

if s:path =~# '/app/jobs/.*\.rb$'
  syn match rubyMacro '\v<%(queue_as)>[!?:]@!'
  syn match rubyExceptionMacro '\v<%(rescue_from|retry_on|discard_on)>[!?:]@!'
  syn match rubyCallback '\v<%(before_enqueue|around_enqueue|after_enqueue|before_perform|around_perform|after_perform)>[!?:]@!'
endif

if s:path =~# '/app/helpers/.*_helper\.rb$\|/app/views/'
  let s:autogenerated_helpers = '
        \ action_name asset_path asset_url atom_feed audio_path audio_tag audio_url auto_discovery_link_tag
        \ button_tag button_to
        \ cache cache_fragment_name cache_if cache_unless caching? capture cdata_section check_box check_box_tag class_names collection_check_boxes collection_radio_buttons collection_select color_field color_field_tag compute_asset_extname compute_asset_host compute_asset_path concat content_for content_for? content_tag controller controller_name controller_path convert_to_model cookies csp_meta_tag csrf_meta_tag csrf_meta_tags current_cycle current_page? cycle
        \ date_field date_field_tag date_select datetime_field datetime_field_tag datetime_local_field datetime_local_field_tag datetime_select debug distance_of_time_in_words distance_of_time_in_words_to_now dom_class dom_id
        \ email_field email_field_tag escape_javascript escape_once excerpt
        \ favicon_link_tag field_id field_name field_set_tag fields fields_for file_field file_field_tag flash font_path font_url form_for form_tag form_with
        \ grouped_collection_select grouped_options_for_select
        \ headers hidden_field hidden_field_tag highlight
        \ image_path image_submit_tag image_tag image_url
        \ j javascript_cdata_section javascript_include_tag javascript_path javascript_tag javascript_url
        \ l label label_tag link_to link_to_if link_to_unless link_to_unless_current localize
        \ mail_to month_field month_field_tag
        \ number_field number_field_tag number_to_currency number_to_human number_to_human_size number_to_percentage number_to_phone number_with_delimiter number_with_precision
        \ option_groups_from_collection_for_select options_for_select options_from_collection_for_select
        \ params password_field password_field_tag path_to_asset path_to_audio path_to_font path_to_image path_to_javascript path_to_stylesheet path_to_video phone_field phone_field_tag phone_to pluralize preload_link_tag provide public_compute_asset_path
        \ radio_button radio_button_tag range_field range_field_tag raw render request request_forgery_protection_token reset_cycle response
        \ safe_concat safe_join sanitize sanitize_css search_field search_field_tag select_date select_datetime select_day select_hour select_minute select_month select_second select_tag select_time select_year session simple_format sms_to strip_links strip_tags stylesheet_link_tag stylesheet_path stylesheet_url submit_tag
        \ t tag telephone_field telephone_field_tag text_area text_area_tag text_field text_field_tag time_ago_in_words time_field time_field_tag time_select time_tag time_zone_options_for_select time_zone_select to_sentence token_list translate truncate
        \ uncacheable! url_field url_field_tag url_for url_to_asset url_to_audio url_to_font url_to_image url_to_javascript url_to_stylesheet url_to_video utf8_enforcer_tag
        \ video_path video_tag video_url
        \ week_field week_field_tag weekday_options_for_select weekday_select word_wrap
        \'[1:-1]
  exe 'syn match rubyViewHelper "\v<%(' . escape(tr(s:autogenerated_helpers, ' ', '|'), '?') . ')[[:keyword:]!?:]@!"'
  syn match rubyViewHelper '\v<select>%([!?:]|\s*[{]|\s*do>|\s*[(]=\s*[&])@!'
  syn match rubyViewHelper '\v\.@<!<%(h|html_escape|u|url_encode)>[!?:]@!'
  syn match rubyViewHelper '\v<%(asset_pack_path|image_pack_tag|javascript_pack_tag|stylesheet_pack_tag)>[!?:]@!'
  syn match rubyViewHelper '\v<action_cable_meta_tag>[!?:]@!'
  if s:path =~# '_[^\/]*$'
    syn match rubyViewHelper '\v<local_assigns>[!?:]@!'
  endif
endif

if s:path =~# '/app/controllers/.*\.rb$'
  syn match rubyHelper '\v<%(params|request|response|session|headers|cookies|flash)>[!?:]@!'
  syn match rubyMacro '\v<%(protect_from_forgery|skip_forgery_protection|http_basic_authenticate_with)>[!?:]@!'
  syn match   rubyMacro '\v<respond_to>\ze[( ] *[:*]'
  syn match   rubyResponse '\v<respond_to>\ze +%([&{]|do>)'
  syn match rubyResponse '\v<%(render|head|redirect_to|redirect_back|redirect_back_or_to|respond_with|send_data|send_file)>[!?:]@!'
  syn match rubyResponse '\v<%(authenticate_or_request_with_http_basic|authenticate_with_http_basic|http_basic_authenticate_or_request_with|request_http_basic_authentication)>[!?:]@!'
endif

let b:rails_path = s:path
if s:path =~# '/app/controllers/.*\.rb$\|/app/mailers/.*\.rb$\|/app/models/.*_mailer\.rb$'
  syn match rubyHelper '\v<%(render_to_string)>[!?:]@!'
  syn match rubyCallback '\v<%(before|append_before|prepend_before|after|append_after|prepend_after|around|append_around|prepend_around|skip_before|skip_after|skip)_action>[!?:]@!'
  syn match rubyMacro '\v<%(helper|helper_attr|helper_method|layout)>[!?:]@!'
  syn match rubyExceptionMacro '\v<%(rescue_from)>[!?:]@!'
endif

if s:path =~# '/app/mailers/.*\.rb$\|/app/models/.*_mailer\.rb$'
  syn match rubyResponse '\v<%(mail|render)>[!?:]@!'
  syn match   rubyResponse '\v<headers>[!?:]@!'
  syn match   rubyHelper '\v<headers\[@='
  syn match rubyHelper '\v<%(params|attachments)>[!?:]@!'
  syn match rubyMacro '\v<%(default)>[!?:]@!'
  syn match rubyMacro '\v<%(register_interceptor|register_interceptors|register_observer|register_observers)>[!?:]@!'
endif

if s:path =~# '/app/\w\+/concerns/.*\.rb$'
  syn match rubyMacro '\v<%(included|class_methods)>[!?:]@!'
endif

if s:path =~# '\v/app/%(controllers|helpers|mailers)/.*\.rb$|/app/views/|/test/(controllers|integration|system)/.*_test\.rb$|/spec/(features|requests)/.*_spec\.rb$'
  syn match rubyUrlHelper '\v<%(url_for|polymorphic_path|polymorphic_url|edit_polymorphic_path|edit_polymorphic_url|new_polymorphic_path|new_polymorphic_url)>[!?:]@!'
endif

if s:path =~# '/db/migrate/.*\.rb$\|/db/schema\.rb$'
  syn match rubySchema '\v<%(create_table|change_table|drop_table|rename_table|create_join_table|drop_join_table)>[!?:]@!'
  syn match rubySchema '\v<%(create_schema|drop_schema)>[!?:]@!'
  syn match rubySchema '\v<%(add_column|rename_column|change_column|change_column_default|change_column_null|remove_column|remove_columns)>[!?:]@!'
  syn match rubySchema '\v<%(add_foreign_key|remove_foreign_key)>[!?:]@!'
  syn match rubySchema '\v<%(add_timestamps|remove_timestamps)>[!?:]@!'
  syn match rubySchema '\v<%(add_reference|remove_reference|add_belongs_to|remove_belongs_to)>[!?:]@!'
  syn match rubySchema '\v<%(add_index|remove_index|rename_index)>[!?:]@!'
  syn match rubySchema '\v<%(enable_extension|reversible|revert)>[!?:]@!'
  syn match rubySchema '\v<%(execute|transaction)>[!?:]@!'
endif

if s:path =~# '\.rake$\|/Rakefile[^/]*$'
  syn match rubyRakeMacro '\v^\s*\zs%(task|file|namespace|desc)>%([!?:]|\s*[=])@!'
endif

if s:path =~# '/config/routes\>.*\.rb$'
  syn match rubyRoute '\v<%(resource|resources|collection|member|new|nested|shallow)>[!?:]@!'
  syn match rubyRoute '\v<%(match|get|put|patch|post|delete|root|mount)>[!?:]@!'
  syn match rubyRoute '\v<%(scope|controller|namespace|constraints|defaults)>[!?:]@!'
  syn match rubyRoute '\v<%(concern|concerns)>[!?:]@!'
  syn match rubyRoute '\v<%(direct|resolve)>[!?:]@!'
  syn match rubyHelper '\v<%(redirect)>[!?:]@!'
endif

if s:path =~# '/test\%(/\|/.*/\)test_[^\/]*\.rb$\|/test/.*_test\.rb$\|/features/step_definitions/.*\.rb$'
  syn match rubyAssertion '\v<%(refute|refute_empty|refute_equal|refute_in_delta|refute_in_epsilon|refute_includes|refute_instance_of|refute_kind_of|refute_match|refute_nil|refute_operator|refute_predicate|refute_respond_to|refute_same)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert|assert_empty|assert_equal|assert_in_delta|assert_in_epsilon|assert_includes|assert_instance_of|assert_kind_of|assert_match|assert_nil|assert_operator|assert_predicate|assert_respond_to|assert_same)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_not|assert_not_empty|assert_not_equal|assert_not_in_delta|assert_not_in_epsilon|assert_not_includes|assert_not_instance_of|assert_not_kind_of|assert_no_match|assert_not_nil|assert_not_operator|assert_not_predicate|assert_not_respond_to|assert_not_same)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_raises|assert_send|assert_throws)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_nothing_raised|assert_not_send|assert_nothing_thrown)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_raise|assert_block|assert_mock|assert_output|assert_raise_with_message|assert_silent)>[!?:]@!'
  syn match rubyAssertion '\v<%(flunk)>[!?:]@!'
endif

if s:path =~# '/spec/.*_spec\.rb$'
  syn match rubyTestHelper '\v<subject>[!?:]@!'
  syn match rubyTestMacro '\v<%(let|given)!=[[:keyword:]!?:]@!'
  syn match rubyTestMacro '\v<subject>!=\ze%(\s*[(]|\s+[{&:]|\s+do\>)'
  syn match rubyTestMacro '\v<%(before|after|around|background|setup|teardown)>[!?:]@!'
  syn match rubyTestMacro '\v<%(context|describe|feature|shared_context|shared_examples|shared_examples_for)>[!?:]@!' containedin=rubyKeywordAsMethod
  syn match rubyTestMacro '\v<%(it|example|specify|scenario|include_examples|include_context|it_should_behave_like|it_behaves_like)>[!?:]@!'
  syn match rubyDebug '\v<%(fcontext|fdescribe)>[!?:]@!' containedin=rubyKeywordAsMethod
  syn match rubyDebug '\v<%(fit|fexample|fspecify)>[!?:]@!'
  syn match rubyComment '\v<%(xcontext|xdescribe|xfeature)>[!?:]@!' containedin=rubyKeywordAsMethod
  syn match rubyComment '\v<%(xit|xexample|xspecify|xscenario)>[!?:]@!'
endif
if s:path =~# '/spec/.*_spec\.rb$\|/features/step_definitions/.*\.rb$'
  syn match rubyAssertion '\v<%(pending|skip|expect|is_expected|expect_any_instance_of|allow|allow_any_instance_of)>[!?:]@!'
  syn match rubyTestHelper '\v<%(described_class)>[!?:]@!'
  syn match rubyTestHelper '\v<%(double|instance_double|class_double|object_double)>[!?:]@!'
  syn match rubyTestHelper '\v<%(spy|instance_spy|class_spy|object_spy)>[!?:]@!'
  syn match rubyTestAction '\v<%(stub_const|hide_const)>[!?:]@!'
endif

if s:path =~# '\v/test/%(channels|controllers|helpers|integration|mailers|models|system)/.*_test\.rb$'
  if s:has_app && !empty(rails#app().user_assertions())
    exe "syn match rubyUserAssertion '\\v<%(" . escape(join(rails#app().user_assertions(), '|'), '?') . ")[[:keyword:]?!:]@!'"
  endif
  syn match rubyTestMacro '\v<%(test|setup|teardown)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_difference|assert_no_difference)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_changes|assert_no_changes)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_emails|assert_enqueued_emails|assert_no_emails|assert_no_enqueued_emails)>[!?:]@!'
  syn match rubyTestAction '\v<%(travel|travel_to|travel_back|freeze_time|unfreeze_time)>[!?:]@!'
endif
if s:path =~# '\v/test/%(controllers|integration|system)/.*_test\.rb$'
  syn match rubyAssertion '\v<%(assert_response|assert_redirected_to|assert_template|assert_recognizes|assert_generates|assert_routing)>[!?:]@!'
endif
if s:path =~# '\v/test/%(controllers|helpers|integration|system)/.*_test\.rb$'
  syn match rubyAssertion '\v<%(assert_dom_equal|assert_dom_not_equal|assert_select|assert_select_encoded|assert_select_email)>[!?:]@!'
  syn match rubyTestHelper '\v<%(css_select)>[!?:]@!'
endif
if s:path =~# '/test/system/.*_test\.rb$' ||
      \ s:has_app && rails#buffer().type_name('test-system')
  syn match rubyAssertion     '\v<%(assert_matches_css|assert_matches_selector|assert_matches_xpath)>[!?:]@!'
  syn match rubyAssertion     '\v<%(refute_matches_css|refute_matches_selector|refute_matches_xpath)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_not_matches_css|assert_not_matches_selector|assert_not_matches_xpath)>[!?:]@!'
  syn match rubyAssertion    '\v<%(assert_button|assert_checked_field|assert_content|assert_css|assert_current_path|assert_field|assert_link|assert_select|assert_selector|assert_table|assert_text|assert_title|assert_unchecked_field|assert_xpath)>[!?:]@!'
  syn match rubyAssertion '\v<%(assert_no_button|assert_no_checked_field|assert_no_content|assert_no_css|assert_no_current_path|assert_no_field|assert_no_link|assert_no_select|assert_no_selector|assert_no_table|assert_no_text|assert_no_title|assert_no_unchecked_field|assert_no_xpath)>[!?:]@!'
  syn match rubyAssertion    '\v<%(refute_button|refute_checked_field|refute_content|refute_css|refute_current_path|refute_field|refute_link|refute_select|refute_selector|refute_table|refute_text|refute_title|refute_unchecked_field|refute_xpath)>[!?:]@!'
endif

if s:path =~# '/spec/controllers/.*_spec\.rb$'
  syn match rubyTestMacro '\v<%(render_views)>[!?:]@!'
  syn match rubyTestHelper '\v<%(assigns)>[!?:]@!'
endif
if s:path =~# '/spec/helpers/.*_spec\.rb$'
  syn match rubyTestAction '\v<%(assign)>[!?:]@!'
  syn match rubyTestHelper '\v<helper>[!?:]@!'
  syn match rubyTestMacro '\v<helper>!=\ze%(\s*[(]|\s+[{&:]|\s+do\>)'
endif
if s:path =~# '/spec/views/.*_spec\.rb$'
  syn match rubyTestAction '\v<%(assign|render)>[!?:]@!'
  syn match rubyTestHelper '\v<%(rendered)>[!?:]@!'
endif

if s:has_app && rails#buffer().type_name('test', 'spec')
  syn match rubyTestMacro '\v<%(fixtures|use_transactional_tests|use_instantiated_fixtures)>[!?:]@!'
  syn match rubyTestHelper '\v<%(file_fixture)>[!?:]@!'
endif
if s:path =~# '\v/test/%(controllers|integration)/.*_test\.rb$|/spec/%(controllers|requests)/.*_spec\.rb$'
  syn match   rubyTestAction '\v\.@<!<%(get|post|put|patch|delete|head|process)>[?!:]@!'
  syn match   rubyTestAction '\v<follow_redirect![[:keyword:]!?:]@!'
  syn match rubyTestAction '\v<%(get_via_redirect|post_via_redirect)>[!?:]@!'
  syn match rubyTestHelper '\v<%(request|response|flash|session|cookies|fixture_file_upload)>[!?:]@!'
endif
if s:path =~# '/test/system/.*_test\.rb$\|/spec/features/.*_spec\.rb$' ||
      \ s:has_app && rails#buffer().type_name('cucumber')
  syn match rubyTestHelper '\v<%(body|current_host|current_path|current_scope|current_url|current_window|html|response_headers|source|status_code|title|windows)>[!?:]@!'
  syn match rubyTestHelper '\v<%(page|text)>[!?:]@!'
  syn match rubyTestHelper '\v<%(all|field_labeled|find|find_all|find_button|find_by_id|find_field|find_link|first)>[!?:]@!'
  syn match rubyTestAction '\v<%(evaluate_script|execute_script|go_back|go_forward|open_new_window|save_and_open_page|save_and_open_screenshot|save_page|save_screenshot|switch_to_frame|switch_to_window|visit|window_opened_by|within|within_element|within_fieldset|within_frame|within_table|within_window)>[!?:]@!'
  syn match   rubyTestAction '\v<reset_session![[:keyword:]!?:]@!'
  syn match rubyTestAction '\v<%(attach_file|check|choose|click_button|click_link|click_link_or_button|click_on|fill_in|select|uncheck|unselect)>[!?:]@!'
endif

if !s:has_app
  finish
endif

syn match rubyAttribute '\v<%(class_attribute)>[!?:]@!'
syn match rubyAttribute '\v<%(attr_internal|attr_internal_accessor|attr_internal_reader|attr_internal_writer)>[!?:]@!'
syn match rubyAttribute '\v<%(cattr_accessor|cattr_reader|cattr_writer|mattr_accessor|mattr_reader|mattr_writer)>[!?:]@!'
syn match rubyAttribute '\v<%(thread_cattr_accessor|thread_cattr_reader|thread_cattr_writer|thread_mattr_accessor|thread_mattr_reader|thread_mattr_writer)>[!?:]@!'
syn match rubyMacro '\v<%(alias_attribute|concern|concerning|delegate|delegate_missing_to|with_options)>[!?:]@!'

let s:special = {
      \ '[': '\[\@=',
      \ ']': '[[.]\@!',
      \ '{': '\%(\s*{\|\s\+do\>\)\@=',
      \ '}': '\%(\s*{\|\s\+do\>\)\@!'}
function! s:highlight(group, ...) abort
  let value = rails#buffer().projected(a:0 ? a:1 : a:group)
  let words = split(join(filter(value, 'type(v:val) == type("")'), ' '))
  call filter(words, 'type(v:val) == type("") && v:val =~# ''^\h\k*[!?]\=[][{}]\=$''')
  if !empty(words)
    exe 'syn match' a:group '"\<\%(' . substitute(
          \ join(words, '\|'),
          \ '[][{}]', '\=get(s:special, submatch(0), submatch(0))', 'g') .
          \ '\)[[:keyword:]!?:]\@!"'
  endif
endfunction

call s:highlight(
      \ rails#buffer().type_name('helper', 'view') ? 'rubyHelper' : 'rubyMacro',
      \ 'keywords')
call s:highlight('rubyMacro')
call s:highlight('rubyAction')
call s:highlight('rubyHelper')
call s:highlight('rubyEntity')
call s:highlight('rubyEntities')
